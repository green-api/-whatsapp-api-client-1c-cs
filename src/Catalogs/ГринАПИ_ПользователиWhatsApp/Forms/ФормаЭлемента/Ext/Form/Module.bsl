
&НаКлиенте
Функция ПолучитьПользователяWhatsApp()
	
	ПараметрыПодключения            = Новый Структура("IdInstance, ApiToken");
	ПараметрыПодключения.IdInstance = Объект.IdInstance;
	ПараметрыПодключения.ApiToken   = Объект.ApiToken;
	
	Возврат ПараметрыПодключения;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	ПользовательWhatsApp = ПолучитьПользователяWhatsApp();
	ПараметрыПодключения = ГринАПИ_Служебный.ПолучитьПараметрыПодключенияИзИсточника(ПользовательWhatsApp);
	СтатусСервиса        = ГринАПИ_Служебный.ПроверитьПодключениеНаСервере(ПараметрыПодключения);
	
	Если СтатусСервиса Тогда
		ТекстСообщения = "Подключение выполнено успешно.";	
	Иначе
		ТекстСообщения = "Не удалось выполнить подключение.";	
	КонецЕсли;  
	
	ПоказатьПредупреждение(, ТекстСообщения);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВсеСообщения(Команда)	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолучитьВсеСообщенияПослеЗакрытияВопроса", ЭтотОбъект);	
	ПоказатьВопрос(ОписаниеОповещения, "Очередь сообщений будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
КонецПроцедуры   

&НаКлиенте
Процедура ПолучитьВсеСообщенияПослеЗакрытияВопроса(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательWhatsApp = ПолучитьПользователяWhatsApp();
	ПараметрыПодключения = ГринАПИ_Служебный.ПолучитьПараметрыПодключенияИзИсточника(ПользовательWhatsApp);	
	КлючФоновогоЗадания  = "ОбработкаПолученияСообщенияПользователяWhatsApp";
	
	ОбработкаПолученияСообщенияАсинхронноНаСервере(ПараметрыПодключения, КлючФоновогоЗадания);
	
	ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗавершено", 2);
	
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеЗавершено() Экспорт 
	
	КлючФоновогоЗадания = "ОбработкаПолученияСообщенияПользователяWhatsApp";
	
	Если ГринАПИ_Служебный.ФоновоеЗаданиеЗапущено(КлючФоновогоЗадания) Тогда			
		Возврат;
	КонецЕсли;
	
	Сообщение       = Новый СообщениеПользователю;
	Сообщение.Текст = "Фоновое задание завершено.";
	Сообщение.Сообщить();	
    
    ОтключитьОбработчикОжидания("ФоновоеЗаданиеЗавершено");    
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПолученияСообщенияАсинхронноНаСервере(ПараметрыПодключения, КлючФоновогоЗадания)	
	
	Если ГринАПИ_Служебный.ФоновоеЗаданиеЗапущено(КлючФоновогоЗадания) Тогда
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Фоновое задание выполняется.";
		Сообщение.Сообщить();		
		
		Возврат;
		
	КонецЕсли;
    
    ПараметрыМассив = Новый Массив;
	ПараметрыМассив.Добавить(ПараметрыПодключения);		
    
    ФоновыеЗадания.Выполнить("ГринАПИ_СистемаВзаимодейтвия.ОбработкаПолученияСообщенияПользователяWhatsApp", 
        ПараметрыМассив, КлючФоновогоЗадания); 
		
	Сообщение       = Новый СообщениеПользователю;
	Сообщение.Текст = "Фоновое задание запущено.";
	Сообщение.Сообщить();	 
    
КонецПроцедуры   

&НаКлиенте
Процедура ОткрытьСсылку(ВебСсылка)
	Оповещение = Новый ОписаниеОповещения("ЗавершитьЗапускПриложенияGR", ЭтотОбъект);
	НачатьЗапускПриложения(Оповещение, ВебСсылка);
КонецПроцедуры   

&НаКлиенте
Процедура ЗавершитьЗапускПриложенияGR(КодВозврата, ДополнительныеПараметры) Экспорт	
	// Действия не требуются	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЛичныйКабинет(Команда)
	ОткрытьСсылку("https://console.green-api.com/instanceList/");	
КонецПроцедуры
