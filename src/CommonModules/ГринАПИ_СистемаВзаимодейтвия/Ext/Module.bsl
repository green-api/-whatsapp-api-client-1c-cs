
Функция ОтправленноеСообщениеВебХук()
	Возврат "outgoingMessageReceived";
КонецФункции

Функция ВходящееСообщениеВебХук()
	Возврат "incomingMessageReceived";
КонецФункции  

Функция ТекстовоеСообщение()
	Возврат "textMessage";
КонецФункции

Функция РасширенноеТекстовоеСообщение()
	Возврат "extendedTextMessage";
КонецФункции 

Функция ЦитированиеСообщения()
	Возврат "quotedMessage";
КонецФункции  

Функция ГолосовоеСообщение()
	Возврат "audioMessage";
КонецФункции

Функция ИзображениеСообщение()
	Возврат "imageMessage";
КонецФункции     

Функция ДокументСообщение()
	Возврат "documentMessage";
КонецФункции 

Функция ПолучитьРасширениеИмениФайла(ИмяФайла) Экспорт
	
	РасширениеФайла = "";
	МассивСтрок     = СтрРазделить(ИмяФайла, ".", Ложь);
	
	Если МассивСтрок.Количество() > 1 Тогда
		РасширениеФайла = МассивСтрок[МассивСтрок.Количество() - 1];
	КонецЕсли;
	
	Возврат РасширениеФайла;
	
КонецФункции

Функция ПравильныйТипВебХука(ТипВебХука)  
	
	Результат = Ложь;
	
	Если ТипВебХука = ОтправленноеСообщениеВебХук() Тогда	
		Результат = Истина;
	КонецЕсли;
	
	Если ТипВебХука = ВходящееСообщениеВебХук() Тогда	
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьТекстВложения(Текст, ЗначениеРезультата)	
	
	Результат = Текст;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда		
		Результат = ЗначениеРезультата;	
	КонецЕсли;	
	
	Возврат Результат;	
	
КонецФункции

Функция ПолучитьСодержимоеСообщения(ТипСообщения, Ответ)
	
	РезультатСтруктура = Новый Структура("Текст, ПутьКФайлу, ИмяФайла");
	
	Если ТипСообщения = ТекстовоеСообщение() Тогда		
		РезультатСтруктура.Текст = Ответ.body.messageData.textMessageData.textMessage;		
	КонецЕсли;	
	
	Если ТипСообщения = РасширенноеТекстовоеСообщение() Тогда		
		РезультатСтруктура.Текст = Ответ.body.messageData.extendedTextMessageData.text;		
	КонецЕсли;		
		
	Если ТипСообщения = ЦитированиеСообщения() Тогда		
		РезультатСтруктура.Текст = Ответ.body.messageData.extendedTextMessageData.text;		
	КонецЕсли;
	
	Если ТипСообщения = ГолосовоеСообщение() Тогда			                                    
		
		ИмяФайла = Ответ.body.messageData.fileMessageData.fileName;
		
		РезультатСтруктура.ПутьКФайлу = Ответ.body.messageData.fileMessageData.downloadUrl;		
		РезультатСтруктура.ИмяФайла   = "Аудиосообщение." + ПолучитьРасширениеИмениФайла(ИмяФайла);
		
	КонецЕсли;	
	
	Если ТипСообщения = ИзображениеСообщение() Тогда											
		
		Текст    = Ответ.body.messageData.fileMessageData.caption;
		ИмяФайла = Ответ.body.messageData.fileMessageData.fileName;	
		
		РезультатСтруктура.ПутьКФайлу = Ответ.body.messageData.fileMessageData.downloadUrl;				
		РезультатСтруктура.ИмяФайла   = ПолучитьТекстВложения(Текст, "Мультимедийное сообщение") + "." + ПолучитьРасширениеИмениФайла(ИмяФайла);
		
	КонецЕсли;  
	
	Если ТипСообщения = ДокументСообщение() Тогда											
		
		Текст    = Ответ.body.messageData.fileMessageData.caption;
		ИмяФайла = Ответ.body.messageData.fileMessageData.fileName;	
		
		Если Не Текст = ИмяФайла Тогда			
			РезультатСтруктура.Текст = Текст;
		КонецЕсли;
		
		РезультатСтруктура.ПутьКФайлу = Ответ.body.messageData.fileMessageData.downloadUrl;				
		РезультатСтруктура.ИмяФайла   = ИмяФайла;
		
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(РезультатСтруктура.Текст) И Не ЗначениеЗаполнено(РезультатСтруктура.ПутьКФайлу) Тогда					                                                      
		
		ОписаниеОшибки = "Не удалось распознать собщение: " + Ответ;		
		ЗаписьЖурналаРегистрации("СистемаВзаимодействия.WhatsApp", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки); 
		
		РезультатСтруктура = Неопределено;			
		
	КонецЕсли;	
	
	Возврат РезультатСтруктура;
	
КонецФункции

Функция ПолучитьАвтора(Обсуждение, ТипВебХука)
	
	Участники = Обсуждение.Участники;
	
	Для Каждого СтрокаУчастники Из Участники Цикл
		
		ПользовательСистемыВзаимодействия = СистемаВзаимодействия.ПолучитьПользователя(СтрокаУчастники);
		ВнешнийПользователь               = ПользовательСистемыВзаимодействия.Внешний;
		
		Если ТипВебХука = ОтправленноеСообщениеВебХук() И ВнешнийПользователь = Ложь Тогда		
			Возврат СтрокаУчастники;
		КонецЕсли;   
		
		Если ТипВебХука = ВходящееСообщениеВебХук() И ВнешнийПользователь = Истина Тогда		
			Возврат СтрокаУчастники;
		КонецЕсли;	
		
	КонецЦикла;
	
КонецФункции 

Процедура ДобавитьВложения(ОбработкаОбъект, Сообщение, СодержимоеСообщения) 	
	
	ПутьКФайлу = СодержимоеСообщения.ПутьКФайлу;
	ИмяФайла   = СодержимоеСообщения.ИмяФайла;
	
	Если Не ЗначениеЗаполнено(ПутьКФайлу) Тогда	
		Возврат;
	КонецЕсли;  
	
	ПутьКФайлу = ОбработкаОбъект.СкачатьФайлПоСсылке(ПутьКФайлу);
	Поток      = ФайловыеПотоки.ОткрытьДляЧтения(ПутьКФайлу);
	
	Сообщение.Вложения.Добавить(Поток, ИмяФайла); 			

КонецПроцедуры 

Функция ПолучитьДанныеСообщенийТаблицаЗначений() Экспорт
	    
    ТаблицаЗначений = Новый ТаблицаЗначений;    
	
	ТаблицаЗначений.Колонки.Добавить("ТипВебХука");
	ТаблицаЗначений.Колонки.Добавить("Контакт");
	ТаблицаЗначений.Колонки.Добавить("КлючОбсуждения");            
    ТаблицаЗначений.Колонки.Добавить("ТекстСообщения");
	
	Возврат ТаблицаЗначений;    
    
КонецФункции

Процедура ОтправитьТекстовоеСообщение(ОбработкаОбъект, НомерТелефона, Текст)
	
	Если Не ЗначениеЗаполнено(Текст) Тогда			
		Возврат;	
	КонецЕсли;
	
	ОбработкаОбъект.ОтправитьТекст(НомерТелефона, Текст); 	

КонецПроцедуры

Процедура ОтправитьФайл(ОбработкаОбъект, НомерТелефона, ВложенияСообщений)  
	
	Если ВложенияСообщений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;  
	
	Для каждого СтрокаВложенияСообщений Из ВложенияСообщений Цикл	
		
		Для Каждого Элемент Из СтрокаВложенияСообщений Цикл				
						
			ИмяВременногоФайла = КаталогВременныхФайлов() + Элемент.Ключ; 
			ДвоичныеДанные     = Элемент.Значение;    
			
			ДвоичныеДанные.Записать(ИмяВременногоФайла);			
			ОбработкаОбъект.ОтправитьВидеоАудиоИзображениеДокумент(НомерТелефона, ИмяВременногоФайла, "");
			
			Попытка
				УдалитьФайлы(ИмяВременногоФайла);	
			Исключение	КонецПопытки;		

		КонецЦикла;					
		
	КонецЦикла;	

КонецПроцедуры

Процедура ОбработкаОтправкиСообщения(КлючОбсуждения, Текст, ВложенияСообщений) Экспорт
	
	Попытка    
		
		Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
			Возврат;
		КонецЕсли;			

		Если ТипЗнч(КлючОбсуждения) = Тип("Строка") Тогда
        	ПараметрыСтруктура = РегистрыСведений.ГринАПИ_ПараметрыПодключения.ПолучитьДанныеКонтакта(КлючОбсуждения); 
		Иначе
			ПараметрыСтруктура = РегистрыСведений.ГринАПИ_ПараметрыПодключения.ПолучитьДанныеКонтактаИзМассива(КлючОбсуждения);
		КонецЕсли;				
		
		Если ПараметрыСтруктура = Неопределено Тогда		
			Возврат;
		КонецЕсли;
		
		ОбработкаОбъект = ГринАПИ_Служебный.ПолучитьОбъектОбработки();
		ЗаполнитьЗначенияСвойств(ОбработкаОбъект, ПараметрыСтруктура); 
		
		НомераТелефонов = ПараметрыСтруктура.НомераТелефонов;
		
		Для каждого НомерТелефона Из НомераТелефонов Цикл			
			ОтправитьТекстовоеСообщение (ОбработкаОбъект, НомерТелефона, Текст);
			ОтправитьФайл(ОбработкаОбъект, НомерТелефона, ВложенияСообщений);					
		КонецЦикла;		
		
	Исключение             
				
		ЗаписьЖурналаРегистрации("СистемаВзаимодействия.WhatsApp", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки());		
		
		Сообщение       = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка отправки сообщения. Подробно в журнале регистрации, событие: СистемаВзаимодействия.WhatsApp";
		Сообщение.Сообщить();	
		
	КонецПопытки;	
  
КонецПроцедуры    

Процедура ОбработкаПолученияСообщений(КлючФоновогоЗадания) Экспорт
	
	Если ГринАПИ_Служебный.ФоновоеЗаданиеЗапущено(КлючФоновогоЗадания) Тогда				
		Возврат;		
	КонецЕсли;
	
	ПараметрыМассив = Новый Массив;
	
	ФоновыеЗадания.Выполнить("ГринАПИ_СистемаВзаимодейтвия.ОбработкаПолученияСообщенийФоновоеЗадание", 
	    ПараметрыМассив, КлючФоновогоЗадания); 	
  
КонецПроцедуры  

Процедура ОбработкаПолученияСообщенийФоновоеЗадание() Экспорт 
		
	Попытка 
		
		Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
			Возврат;
		КонецЕсли;		
		
		ДанныеКонтактов = РегистрыСведений.ГринАПИ_ПараметрыПодключения.ПолучитьДанныеКонтактов(); 
		
		Если ДанныеКонтактов = Неопределено Тогда		
			Возврат;
		КонецЕсли;
		
		ДанныеПользователиWhatsApp = ДанныеКонтактов.Скопировать();		
		ДанныеПользователиWhatsApp.Свернуть("idInstance, ApiToken");
		
		ПараметрыПодключения = Новый Структура("idInstance, ApiToken, ДанныеКонтактов");
		
		Для каждого СтрокаДанныеПользователиWhatsApp Из ДанныеПользователиWhatsApp Цикл
			
			ЗаполнитьЗначенияСвойств(ПараметрыПодключения, СтрокаДанныеПользователиWhatsApp); 
		    ПараметрыПодключения.ДанныеКонтактов = ДанныеКонтактов;
			
			ОбработкаПолученияСообщенияПользователяWhatsApp(ПараметрыПодключения, Истина);
		
		КонецЦикла;	
		
	Исключение             				
		ЗаписьЖурналаРегистрации("СистемаВзаимодействия.WhatsApp", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки());				
	КонецПопытки;	
  
КонецПроцедуры

Процедура ОбработкаПолученияСообщенияПользователяWhatsApp(ПараметрыПодключения, ЗаполнятьБеседу = Ложь) Экспорт 
	
	Попытка 
		
		Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
			Возврат;
		КонецЕсли;						
		
		ОбработкаОбъект = ГринАПИ_Служебный.ПолучитьОбъектОбработки();
		ЗаполнитьЗначенияСвойств(ОбработкаОбъект, ПараметрыПодключения);
		
		Если ЗаполнятьБеседу Тогда		
			ДанныеКонтактов = ПараметрыПодключения.ДанныеКонтактов;
		КонецЕсли;				
		
		Пока Истина Цикл 
			
			Ответ = ОбработкаОбъект.ПолучитьСообщение();
			
			Если Не ЗначениеЗаполнено(Ответ) Тогда		
				Прервать;
			КонецЕсли;	
			
			Если Не ЗаполнятьБеседу Тогда				
				Продолжить;
			КонецЕсли; 
			
			ТипВебХука = Ответ.body.typeWebhook;
			
			Если Не ПравильныйТипВебХука(ТипВебХука) Тогда				
				Продолжить;
			КонецЕсли;
			
			НомерТелефона 	= ГринАПИ_Служебный.ПолучитьНомерТелефона(Ответ.body.senderData.chatId);			
			НайденнаяСтрока = ДанныеКонтактов.Найти(НомерТелефона, "НомерТелефона");
			
			Если НайденнаяСтрока = Неопределено Тогда		
				Продолжить;	
			КонецЕсли; 
			
			ТипСообщения        = Ответ.body.messageData.typeMessage;
			СодержимоеСообщения = ПолучитьСодержимоеСообщения(ТипСообщения, Ответ);
			
			Если СодержимоеСообщения = Неопределено Тогда			
				Продолжить;
			КонецЕсли;
			
			КлючОбсуждения = НайденнаяСтрока.КлючОбсуждения;			
			Обсуждение 	   = СистемаВзаимодействия.ПолучитьОбсуждение(КлючОбсуждения); 
			
			Если Обсуждение = Неопределено Тогда
				Продолжить;
			КонецЕсли;	
			
			Сообщение       = СистемаВзаимодействия.СоздатьСообщение(Обсуждение.Идентификатор);
			Сообщение.Автор = ПолучитьАвтора(Обсуждение, ТипВебХука);			 
			Сообщение.Текст = СодержимоеСообщения.Текст;             
			
			ДобавитьВложения(ОбработкаОбъект, Сообщение, СодержимоеСообщения);			
			
			Сообщение.Записать();			
			
		КонецЦикла;				
		
	Исключение		
		ЗаписьЖурналаРегистрации("СистемаВзаимодействия.WhatsApp", УровеньЖурналаРегистрации.Ошибка, , , ОписаниеОшибки());
	КонецПопытки;	
	
КонецПроцедуры  